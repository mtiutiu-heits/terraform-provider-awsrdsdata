---
page_title: "Provider: AWSRDSDATA"
description: |-
  A Terraform provider that uses the Amazon RDS data service to provision SQL users and grant privileges (only MySQL is supported for now).
---

# AWSRDSDATA Provider

The **AWSRDSDATA** provider provides utilities for working with users and privileges on MySQL clusters (Aurora V1) 
via the [RDS data service](https://docs.aws.amazon.com/AmazonRDS/latest/AuroraUserGuide/data-api.html) API.
It provides resources that allow MySQL users and privileges to be created as part of a Terraform deployment.

**Important Note:**

Make sure to **enable the HTTP endpoint** for the RDS data service (e.g. set **`enable_http_endpoint`** to **`true`** for the **`aws_rds_cluster`** resource).

Use the navigation to the left to read about the available resources.

## Example Usage

```terraform
provider "awsrdsdata" {
  region = "us-east-1" # optional
}

# Provision credentials for the master DB acount
resource "random_password" "master_password" {
  length  = 16
  special = false
}

# Also, store sensitive data in a dedicated AWS secret
resource "aws_secretsmanager_secret" "master_db_credentials" {
  name = "master_db_credentials"
}

resource "aws_secretsmanager_secret_version" "master_db_credentials" {
  secret_id = aws_secretsmanager_secret.master_db_credentials.id
  secret_string = jsonencode(
    {
      username = aws_rds_cluster.mysql_instance.master_username
      password = aws_rds_cluster.mysql_instance.master_password
      host     = aws_rds_cluster.mysql_instance.endpoint
      port     = aws_rds_cluster.mysql_instance.port
    }
  )
}

resource "aws_rds_cluster" "mysql_instance" {
  cluster_identifier      = "aurora-mysql-cluster-demo"
  engine                  = "aurora-mysql"
  engine_version          = "5.7.mysql_aurora.2.03.2"
  availability_zones      = ["us-west-2a", "us-west-2b", "us-west-2c"]
  database_name           = "test"
  master_username         = "master"
  master_password         = random_password.master_password.result
  enable_http_endpoint    = true # <- this is very important
}

# Provision credentials for the MySQL DB acount used to test the provider
resource "random_password" "test_account_password" {
  length  = 16
  special = false
}

# Also, store sensitive data in a dedicated AWS secret
resource "aws_secretsmanager_secret" "test_account_db_credentials" {
  name = "test_account_db_credentials"
}

resource "aws_secretsmanager_secret_version" "test_account_db_credentials" {
  secret_id = aws_secretsmanager_secret.test_account_db_credentials.id
  secret_string = jsonencode(
    {
      username = awsrdsdata_mysql_user.test_account.user
      password = awsrdsdata_mysql_user.test_account.password
    }
  )
}

resource "awsrdsdata_mysql_user" "test_account" {
  user                  = "test"
  host                  = "%"
  password              = random_password.test_account_password.result
  database_resource_arn = aws_rds_cluster.mysql_instance.arn
  database_secret_arn   = aws_secretsmanager_secret.master_db_credentials.arn
}

resource "awsrdsdata_mysql_grant" "permissions" {
  user                  = awsrdsdata_mysql_user.test_account.user
  host                  = awsrdsdata_mysql_user.test_account.host
  database              = aws_rds_cluster.mysql_instance.database_name
  privileges            = ["SELECT", "INSERT", "UPDATE"]
  database_resource_arn = aws_rds_cluster.mysql_instance.arn
  database_secret_arn   = aws_secretsmanager_secret.master_db_credentials.arn
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `region` (String) The RDS data service AWS region
